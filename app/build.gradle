apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    signingConfigs {
        staging {
            storeFile file('../staging_keystore.jks')
            storePassword STAGING_KEYSTORE_PASSWORD
            keyAlias STAGING_KEY_ALIAS_NAME
            keyPassword STAGING_KEY_ALIAS_PASSWORD
        }
    }

    compileSdkVersion 29
    buildToolsVersion "29.0.3"
    defaultConfig {
        applicationId "com.quickhandslogistics"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 2
        versionName "1.0.40"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        lintOptions {
            checkReleaseBuilds false
        }
    }

    buildTypes {
        applicationVariants.all { variant ->
            variant.outputs.all {
                def flavor = variant.name
                outputFileName = "quick_hands_${flavor}_${versionName}.apk"
            }
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "default"
    productFlavors {
        devlopment {
            buildConfigField "String", "API_BASE_URL", DEV_API_URL
        }
        qa {
            buildConfigField "String", "API_BASE_URL", QA_API_URL
        }
        staging {
            buildConfigField "String", "API_BASE_URL", STAGING_API_URL
            signingConfig signingConfigs.staging
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Default
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    //Testing
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test:core:1.3.0-rc01'
    androidTestImplementation 'androidx.test:core-ktx:1.3.0-rc01'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.ext:junit-ktx:1.1.1'
    androidTestImplementation 'androidx.test:rules:1.3.0-rc01'
    androidTestImplementation 'androidx.test:runner:1.3.0-rc01'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.2.0'

    // Native UI Elements
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:26.0.0')

    // Declare the dependencies for the Crashlytics and Analytics libraries
    // When using the BoM, you don't specify versions in Firebase library dependencies
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-messaging:21.0.0'

    // Multiple Screen Dimensions Sizes
    implementation 'com.intuit.sdp:sdp-android:1.0.6'
    implementation 'com.intuit.ssp:ssp-android:1.0.6'

    // Circular ImageView
    implementation 'de.hdodenhof:circleimageview:3.1.0'

    // Network
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.7.2'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    // Horizontal Calendar View
    implementation 'com.michalsvec:single-row-calednar:1.0.0'

    // Multiple Language Support
    implementation 'com.github.franmontiel:LocaleChanger:0.9.2'

    // Image Processing
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'
    implementation 'com.jsibbold:zoomage:1.2.0'

    // Flexible Layout
    implementation 'com.google.android:flexbox:2.0.1'

    // Signature
    implementation 'com.github.gcacace:signature-pad:1.3.1'

    // Custom Alert Dialog
    implementation 'com.github.sahilgarg90:sweet-alert-dialog:2.5'

    implementation 'com.otaliastudios:zoomlayout:1.8.0'
}
